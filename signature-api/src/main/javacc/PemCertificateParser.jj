PARSER_BEGIN(PemCertificateParser)
/*
 * Licensed to the Apache Software Foundation (ASF) under one or more
 * contributor license agreements.  See the NOTICE file distributed with
 * this work for additional information regarding copyright ownership.
 * The ASF licenses this file to You under the Apache License, Version 2.0
 * (the "License"); you may not use this file except in compliance with
 * the License.  You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
package org.apache.amber.signature.signers.rsa;

import java.io.ByteArrayOutputStream;
import java.io.IOException;

import org.apache.commons.codec.binary.Base64OutputStream;

/**
 * PEM private/public certificates parser.
 *
 * @version $Id$
 */
final class PemCertificateParser {

    private final ByteArrayOutputStream baos = new ByteArrayOutputStream();

    private final Base64OutputStream output = new Base64OutputStream(baos, false);

}

PARSER_END(PemCertificateParser)

SKIP : {
    <[" ","\t","\n","\r"]>
}

TOKEN : {
    <BEGIN_PRIVATE_KEY : "-----BEGIN PRIVATE KEY-----">
    | <END_PRIVATE_KEY : "-----END PRIVATE KEY-----">
    | <BEGIN_CERTIFICATE : "-----BEGIN CERTIFICATE-----">
    | <END_CERTIFICATE : "-----END CERTIFICATE-----">
    | <CERTIFICATE_CONTENT : (["a"-"z","A"-"Z","0"-"9","+","/","="])+>
}

public byte[] parsePrivateKey() : { }
{
    <BEGIN_PRIVATE_KEY> [ certificateContent() ]  <END_PRIVATE_KEY> <EOF> {
        try {
            this.output.close();
            this.baos.close();
            return this.baos.toByteArray();
        } catch (IOException e) {
            throw new ParseException("Fatal protocol error: " + e.getMessage());
        }
    }
}

public byte[] parsePublicCertificate() : { }
{
    <BEGIN_CERTIFICATE> [ certificateContent() ]  <END_CERTIFICATE> <EOF> {
        try {
            this.output.close();
            this.baos.close();
            return this.baos.toByteArray();
        } catch (IOException e) {
            throw new ParseException("Fatal protocol error: " + e.getMessage());
        }
    }
}

private void certificateContent() : {}
{
    content() [ certificateContent() ]
}

private void content() : { Token t; }
{
    t=<CERTIFICATE_CONTENT> {
        byte[] buffer = t.toString().getBytes();
        try {
            this.output.write(buffer, 0, buffer.length);
        } catch (IOException e) {
            throw new ParseException("Fatal protocol error: " + e.getMessage());
        }
    }
}
